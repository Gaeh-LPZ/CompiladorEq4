Programa -> UnidadCompilacion EOF { Programa.trad = UnidadCompilacion.trad }
UnidadCompilacion -> DeclaracionPaquete DeclaracionesImportacion DeclaracionesTipo { UnidadCompilacion.trad = DeclaracionPaquete.trad + "\n" + DeclaracionesImportacion.trad + "\n" + DeclaracionesTipo.trad }
UnidadCompilacion -> DeclaracionesImportacion DeclaracionesTipo { UnidadCompilacion.trad = DeclaracionesImportacion.trad + "\n" + DeclaracionesTipo.trad }
DeclaracionPaquete -> PACKAGE NombreCalificado PUNTO_Y_COMA { DeclaracionPaquete.trad = "// Package: " + NombreCalificado.trad }
DeclaracionPaquete -> ε { DeclaracionPaquete.trad = "" }
DeclaracionesImportacion -> DeclaracionImportacion DeclaracionesImportacion { DeclaracionesImportacion.trad = DeclaracionImportacion.trad + "\n" + DeclaracionesImportacion.trad }
DeclaracionesImportacion -> ε { DeclaracionesImportacion.trad = "#include <iostream>\n#include <string>\nusing namespace std;" }
DeclaracionImportacion -> IMPORT NombreCalificado PUNTO_Y_COMA { DeclaracionImportacion.trad = "// import " + NombreCalificado.trad }
DeclaracionImportacion -> IMPORT NombreCalificado PUNTO ASTERISK PUNTO_Y_COMA { DeclaracionImportacion.trad = "// import " + NombreCalificado.trad + ".*" }
DeclaracionesTipo -> DeclaracionTipo DeclaracionesTipo { DeclaracionesTipo.trad = DeclaracionTipo.trad + "\n" + DeclaracionesTipo.trad }
DeclaracionesTipo -> ε { DeclaracionesTipo.trad = "" }
DeclaracionTipo -> DeclaracionClase { DeclaracionTipo.trad = DeclaracionClase.trad }
DeclaracionTipo -> DeclaracionInterfaz { DeclaracionTipo.trad = DeclaracionInterfaz.trad }
DeclaracionTipo -> DeclaracionEnumeracion { DeclaracionTipo.trad = DeclaracionEnumeracion.trad }
DeclaracionTipo -> PUNTO_Y_COMA { DeclaracionTipo.trad = "" }
Modificadores -> Modificador Modificadores { Modificadores.trad = Modificador.trad + " " + Modificadores.trad }
Modificadores -> ε { Modificadores.trad = "" }
Modificador -> PUBLIC { Modificador.trad = "public:" }
Modificador -> PRIVATE { Modificador.trad = "private:" }
Modificador -> PROTECTED { Modificador.trad = "protected:" }
Modificador -> STATIC { Modificador.trad = "static" }
Modificador -> FINAL { Modificador.trad = "const" }
Modificador -> ABSTRACT { Modificador.trad = "virtual" }
Modificador -> SYNCHRONIZED { Modificador.trad = "" }
Modificador -> NATIVE { Modificador.trad = "" }
Modificador -> TRANSIENT { Modificador.trad = "" }
Modificador -> VOLATILE { Modificador.trad = "volatile" }
DeclaracionClase -> Modificadores CLASS IDENTIFICADOR ParametrosTipoOpt ClausulaExtiendeOpt ClausulaImplementaOpt CuerpoClase { DeclaracionClase.trad = "\nclass " + IDENTIFICADOR.lexema + ClausulaExtiendeOpt.trad + " " + CuerpoClase.trad + ";" }
Tipo -> TipoPrimitivo DimensionesTipoOpt { Tipo.trad = TipoPrimitivo.trad + DimensionesTipoOpt.trad }
Tipo -> TipoClaseOInterfaz DimensionesTipoOpt { Tipo.trad = TipoClaseOInterfaz.trad + DimensionesTipoOpt.trad }
TipoPrimitivo -> INT { TipoPrimitivo.trad = "int" }
TipoPrimitivo -> FLOAT { TipoPrimitivo.trad = "float" }
TipoPrimitivo -> DOUBLE { TipoPrimitivo.trad = "double" }
TipoPrimitivo -> BOOLEAN { TipoPrimitivo.trad = "bool" }
TipoPrimitivo -> CHAR { TipoPrimitivo.trad = "char" }
TipoPrimitivo -> BYTE { TipoPrimitivo.trad = "char" }
TipoPrimitivo -> SHORT { TipoPrimitivo.trad = "short" }
TipoPrimitivo -> LONG { TipoPrimitivo.trad = "long" }
NombreCalificado -> IDENTIFICADOR { NombreCalificado.trad = IDENTIFICADOR.lexema }
NombreCalificado -> NombreCalificado PUNTO IDENTIFICADOR { NombreCalificado.trad = NombreCalificado.trad + "::" + IDENTIFICADOR.lexema }
NombreDeExpresion -> IDENTIFICADOR { NombreDeExpresion.trad = IDENTIFICADOR.lexema }
NombreDeExpresion -> NombreDeExpresion PUNTO IDENTIFICADOR { NombreDeExpresion.trad = NombreDeExpresion.trad + "." + IDENTIFICADOR.lexema }
Expresion -> Asignacion { Expresion.trad = Asignacion.trad }
Asignacion -> LadoIzquierdo ASIGNACION Expresion { Asignacion.trad = LadoIzquierdo.trad + " = " + Expresion.trad }
Asignacion -> ExpresionCondicional { Asignacion.trad = ExpresionCondicional.trad }
ExpresionCondicional -> ExpresionOrCondicional { ExpresionCondicional.trad = ExpresionOrCondicional.trad }
ExpresionCondicional -> ExpresionOrCondicional QUESTION Expresion DOS_PUNTOS ExpresionCondicional { ExpresionCondicional.trad = ExpresionOrCondicional.trad + " ? " + Expresion.trad + " : " + ExpresionCondicional.trad }
ExpresionOrCondicional -> ExpresionAndCondicional { ExpresionOrCondicional.trad = ExpresionAndCondicional.trad }
ExpresionOrCondicional -> ExpresionOrCondicional OR ExpresionAndCondicional { ExpresionOrCondicional.trad = ExpresionOrCondicional.trad + " || " + ExpresionAndCondicional.trad }
ExpresionAndCondicional -> ExpresionOrInclusivo { ExpresionAndCondicional.trad = ExpresionOrInclusivo.trad }
ExpresionAndCondicional -> ExpresionAndCondicional AND ExpresionOrInclusivo { ExpresionAndCondicional.trad = ExpresionAndCondicional.trad + " && " + ExpresionOrInclusivo.trad }
ExpresionOrInclusivo -> ExpresionOrExclusivo { ExpresionOrInclusivo.trad = ExpresionOrExclusivo.trad }
ExpresionOrInclusivo -> ExpresionOrInclusivo BITOR ExpresionOrExclusivo { ExpresionOrInclusivo.trad = ExpresionOrInclusivo.trad + " | " + ExpresionOrExclusivo.trad }
ExpresionOrExclusivo -> ExpresionAnd { ExpresionOrExclusivo.trad = ExpresionAnd.trad }
ExpresionOrExclusivo -> ExpresionOrExclusivo BITXOR ExpresionAnd { ExpresionOrExclusivo.trad = ExpresionOrExclusivo.trad + " ^ " + ExpresionAnd.trad }
ExpresionAnd -> ExpresionIgualdad { ExpresionAnd.trad = ExpresionIgualdad.trad }
ExpresionAnd -> ExpresionAnd BITAND ExpresionIgualdad { ExpresionAnd.trad = ExpresionAnd.trad + " & " + ExpresionIgualdad.trad }
ExpresionIgualdad -> ExpresionRelacional { ExpresionIgualdad.trad = ExpresionRelacional.trad }
ExpresionIgualdad -> ExpresionIgualdad IGUAL ExpresionRelacional { ExpresionIgualdad.trad = ExpresionIgualdad.trad + " == " + ExpresionRelacional.trad }
ExpresionIgualdad -> ExpresionIgualdad DIFERENTE ExpresionRelacional { ExpresionIgualdad.trad = ExpresionIgualdad.trad + " != " + ExpresionRelacional.trad }
ExpresionRelacional -> ExpresionDesplazamiento { ExpresionRelacional.trad = ExpresionDesplazamiento.trad }
ExpresionRelacional -> ExpresionRelacional MENOR_QUE ExpresionDesplazamiento { ExpresionRelacional.trad = ExpresionRelacional.trad + " < " + ExpresionDesplazamiento.trad }
ExpresionRelacional -> ExpresionRelacional MAYOR_QUE ExpresionDesplazamiento { ExpresionRelacional.trad = ExpresionRelacional.trad + " > " + ExpresionDesplazamiento.trad }
ExpresionRelacional -> ExpresionRelacional MENOR_IGUAL ExpresionDesplazamiento { ExpresionRelacional.trad = ExpresionRelacional.trad + " <= " + ExpresionDesplazamiento.trad }
ExpresionRelacional -> ExpresionRelacional MAYOR_IGUAL ExpresionDesplazamiento { ExpresionRelacional.trad = ExpresionRelacional.trad + " >= " + ExpresionDesplazamiento.trad }
ExpresionRelacional -> ExpresionRelacional INSTANCEOF Tipo { ExpresionRelacional.trad = ExpresionRelacional.trad }
ExpresionDesplazamiento -> ExpresionAditiva { ExpresionDesplazamiento.trad = ExpresionAditiva.trad }
ExpresionDesplazamiento -> ExpresionDesplazamiento LSHIFT ExpresionAditiva { ExpresionDesplazamiento.trad = ExpresionDesplazamiento.trad + " << " + ExpresionAditiva.trad }
ExpresionDesplazamiento -> ExpresionDesplazamiento RSHIFT ExpresionAditiva { ExpresionDesplazamiento.trad = ExpresionDesplazamiento.trad + " >> " + ExpresionAditiva.trad }
ExpresionDesplazamiento -> ExpresionDesplazamiento URSHIFT ExpresionAditiva { ExpresionDesplazamiento.trad = ExpresionDesplazamiento.trad + " >> " + ExpresionAditiva.trad }
ExpresionAditiva -> ExpresionMultiplicativa { ExpresionAditiva.trad = ExpresionMultiplicativa.trad }
ExpresionAditiva -> ExpresionAditiva SUMA ExpresionMultiplicativa { ExpresionAditiva.trad = ExpresionAditiva.trad + " + " + ExpresionMultiplicativa.trad }
ExpresionAditiva -> ExpresionAditiva RESTA ExpresionMultiplicativa { ExpresionAditiva.trad = ExpresionAditiva.trad + " - " + ExpresionMultiplicativa.trad }
ExpresionMultiplicativa -> ExpresionUnaria { ExpresionMultiplicativa.trad = ExpresionUnaria.trad }
ExpresionMultiplicativa -> ExpresionMultiplicativa ASTERISK ExpresionUnaria { ExpresionMultiplicativa.trad = ExpresionMultiplicativa.trad + " * " + ExpresionUnaria.trad }
ExpresionMultiplicativa -> ExpresionMultiplicativa DIVISION ExpresionUnaria { ExpresionMultiplicativa.trad = ExpresionMultiplicativa.trad + " / " + ExpresionUnaria.trad }
ExpresionMultiplicativa -> ExpresionMultiplicativa MOD ExpresionUnaria { ExpresionMultiplicativa.trad = ExpresionMultiplicativa.trad + " % " + ExpresionUnaria.trad }
ExpresionUnaria -> SUMA ExpresionUnaria { ExpresionUnaria.trad = "+" + ExpresionUnaria.trad }
ExpresionUnaria -> RESTA ExpresionUnaria { ExpresionUnaria.trad = "-" + ExpresionUnaria.trad }
ExpresionUnaria -> NOT ExpresionUnaria { ExpresionUnaria.trad = "!" + ExpresionUnaria.trad }
ExpresionUnaria -> TILDE ExpresionUnaria { ExpresionUnaria.trad = "~" + ExpresionUnaria.trad }
ExpresionUnaria -> ExpresionPostfija { ExpresionUnaria.trad = ExpresionPostfija.trad }
ExpresionPostfija -> Primario OperadorPostfijoOpt { ExpresionPostfija.trad = Primario.trad + OperadorPostfijoOpt.trad }
OperadorPostfijoOpt -> OperadorPostfijo OperadorPostfijoOpt { OperadorPostfijoOpt.trad = OperadorPostfijo.trad + OperadorPostfijoOpt.trad }
OperadorPostfijoOpt -> ε { OperadorPostfijoOpt.trad = "" }
OperadorPostfijo -> PARENTESIS_IZQ ListaArgumentosOpt PARENTESIS_DER { OperadorPostfijo.trad = "(" + ListaArgumentosOpt.trad + ")" }
OperadorPostfijo -> PUNTO IDENTIFICADOR { OperadorPostfijo.trad = "." + IDENTIFICADOR.lexema }
OperadorPostfijo -> PUNTO CLASS { OperadorPostfijo.trad = "" }
OperadorPostfijo -> CORCHETE_IZQ Expresion CORCHETE_DER { OperadorPostfijo.trad = "[" + Expresion.trad + "]" }
Primario -> LITERAL_ENTERA { Primario.trad = LITERAL_ENTERA.lexema }
Primario -> LITERAL_FLOTANTE { Primario.trad = LITERAL_FLOTANTE.lexema }
Primario -> LITERAL_CADENA { Primario.trad = LITERAL_CADENA.lexema }
Primario -> TRUE { Primario.trad = "true" }
Primario -> FALSE { Primario.trad = "false" }
Primario -> NULL { Primario.trad = "nullptr" }
Primario -> PARENTESIS_IZQ Expresion PARENTESIS_DER { Primario.trad = "(" + Expresion.trad + ")" }
Primario -> THIS { Primario.trad = "this" }
Primario -> SUPER MiembroSuperOpt { Primario.trad = "super" + MiembroSuperOpt.trad }
Primario -> IDENTIFICADOR { Primario.trad = IDENTIFICADOR.lexema }
Primario -> NEW Tipo RestoCreadorDeArreglo { Primario.trad = "new " + Tipo.trad + RestoCreadorDeArreglo.trad }
Primario -> NEW TipoClaseOInterfaz PARENTESIS_IZQ ListaArgumentosOpt PARENTESIS_DER CuerpoClaseOpt { Primario.trad = "new " + TipoClaseOInterfaz.trad + "(" + ListaArgumentosOpt.trad + ")" }
MiembroSuperOpt -> PUNTO IDENTIFICADOR PARENTESIS_IZQ ListaArgumentosOpt PARENTESIS_DER { MiembroSuperOpt.trad = "." + IDENTIFICADOR.lexema + "(" + ListaArgumentosOpt.trad + ")" }
MiembroSuperOpt -> ε { MiembroSuperOpt.trad = "" }
LadoIzquierdo -> NombreDeExpresion { LadoIzquierdo.trad = NombreDeExpresion.trad }
LadoIzquierdo -> AccesoCampo { LadoIzquierdo.trad = AccesoCampo.trad }
LadoIzquierdo -> AccesoArreglo { LadoIzquierdo.trad = AccesoArreglo.trad }
AccesoCampo -> Primario PUNTO IDENTIFICADOR { AccesoCampo.trad = Primario.trad + "." + IDENTIFICADOR.lexema }
AccesoArreglo -> Primario CORCHETE_IZQ Expresion CORCHETE_DER { AccesoArreglo.trad = Primario.trad + "[" + Expresion.trad + "]" }
Bloque -> LLAVE_IZQ SentenciasBloqueOpt LLAVE_DER { Bloque.trad = "{\n" + SentenciasBloqueOpt.trad + "\n}" }
SentenciasBloqueOpt -> SentenciasBloque { SentenciasBloqueOpt.trad = SentenciasBloque.trad }
SentenciasBloqueOpt -> ε { SentenciasBloqueOpt.trad = "" }
SentenciasBloque -> SentenciaBloque SentenciasBloque { SentenciasBloque.trad = SentenciaBloque.trad + "\n" + SentenciasBloque.trad }
SentenciasBloque -> SentenciaBloque { SentenciasBloque.trad = SentenciaBloque.trad }
SentenciaBloque -> SentenciaDeclaracionVarLocal { SentenciaBloque.trad = SentenciaDeclaracionVarLocal.trad }
SentenciaBloque -> Sentencia { SentenciaBloque.trad = Sentencia.trad }
SentenciaDeclaracionVarLocal -> Modificadores Tipo DeclaradoresDeVariable PUNTO_Y_COMA { SentenciaDeclaracionVarLocal.trad = Tipo.trad + " " + DeclaradoresDeVariable.trad + ";" }
DeclaradoresDeVariable -> DeclaradorVariable { DeclaradoresDeVariable.trad = DeclaradorVariable.trad }
DeclaradoresDeVariable -> DeclaradorVariable COMA DeclaradoresDeVariable { DeclaradoresDeVariable.trad = DeclaradorVariable.trad + ", " + DeclaradoresDeVariable.trad }
DeclaradorVariable -> IDENTIFICADOR DimensionesVarOpt InicializacionVarOpt { DeclaradorVariable.trad = IDENTIFICADOR.lexema + DimensionesVarOpt.trad + InicializacionVarOpt.trad }
DimensionesVarOpt -> CORCHETE_IZQ CORCHETE_DER DimensionesVarOpt { DimensionesVarOpt.trad = "*" + DimensionesVarOpt.trad }
DimensionesVarOpt -> ε { DimensionesVarOpt.trad = "" }
InicializacionVarOpt -> ASIGNACION InicializadorVariable { InicializacionVarOpt.trad = " = " + InicializadorVariable.trad }
InicializacionVarOpt -> ε { InicializacionVarOpt.trad = "" }
InicializadorVariable -> Expresion { InicializadorVariable.trad = Expresion.trad }
InicializadorVariable -> InicializadorArreglo { InicializadorVariable.trad = InicializadorArreglo.trad }
InicializadorArreglo -> LLAVE_IZQ ListaInicializacionArregloOpt LLAVE_DER { InicializadorArreglo.trad = "{" + ListaInicializacionArregloOpt.trad + "}" }
ListaInicializacionArregloOpt -> ListaInicializacionArreglo { ListaInicializacionArregloOpt.trad = ListaInicializacionArreglo.trad }
ListaInicializacionArregloOpt -> ε { ListaInicializacionArregloOpt.trad = "" }
ListaInicializacionArreglo -> InicializadorVariable { ListaInicializacionArreglo.trad = InicializadorVariable.trad }
ListaInicializacionArreglo -> InicializadorVariable COMA ListaInicializacionArreglo { ListaInicializacionArreglo.trad = InicializadorVariable.trad + ", " + ListaInicializacionArreglo.trad }
Sentencia -> Bloque { Sentencia.trad = Bloque.trad }
Sentencia -> IF PARENTESIS_IZQ Expresion PARENTESIS_DER Sentencia ELSE Sentencia { Sentencia.trad = "if (" + Expresion.trad + ") " + Sentencia.trad + " else " + Sentencia2.trad }
Sentencia -> IF PARENTESIS_IZQ Expresion PARENTESIS_DER Sentencia { Sentencia.trad = "if (" + Expresion.trad + ") " + Sentencia.trad }
Sentencia -> WHILE PARENTESIS_IZQ Expresion PARENTESIS_DER Sentencia { Sentencia.trad = "while (" + Expresion.trad + ") " + Sentencia.trad }
Sentencia -> FOR PARENTESIS_IZQ InicializacionParaOpt PUNTO_Y_COMA CondicionParaOpt PUNTO_Y_COMA ActualizacionParaOpt PARENTESIS_DER Sentencia { Sentencia.trad = "for (" + InicializacionParaOpt.trad + "; " + CondicionParaOpt.trad + "; " + ActualizacionParaOpt.trad + ") " + Sentencia.trad }
Sentencia -> RETURN ExpresionOpt PUNTO_Y_COMA { Sentencia.trad = "return " + ExpresionOpt.trad + ";" }
Sentencia -> SentenciaDeExpresion { Sentencia.trad = SentenciaDeExpresion.trad }
Sentencia -> PUNTO_Y_COMA { Sentencia.trad = ";" }
IDENTIFICADOR_OPC -> IDENTIFICADOR { IDENTIFICADOR_OPC.trad = IDENTIFICADOR.lexema }
IDENTIFICADOR_OPC -> ε { IDENTIFICADOR_OPC.trad = "" }
ExpresionOpt -> Expresion { ExpresionOpt.trad = Expresion.trad }
ExpresionOpt -> ε { ExpresionOpt.trad = "" }
InicializacionParaOpt -> InicializacionPara { InicializacionParaOpt.trad = InicializacionPara.trad }
InicializacionParaOpt -> ε { InicializacionParaOpt.trad = "" }
InicializacionPara -> DeclaracionVarLocal { InicializacionPara.trad = DeclaracionVarLocal.trad }
InicializacionPara -> ListaExpresiones { InicializacionPara.trad = ListaExpresiones.trad }
DeclaracionVarLocal -> Modificadores Tipo DeclaradoresDeVariable { DeclaracionVarLocal.trad = Tipo.trad + " " + DeclaradoresDeVariable.trad }
CondicionParaOpt -> Expresion { CondicionParaOpt.trad = Expresion.trad }
CondicionParaOpt -> ε { CondicionParaOpt.trad = "" }
ActualizacionParaOpt -> ListaExpresiones { ActualizacionParaOpt.trad = ListaExpresiones.trad }
ActualizacionParaOpt -> ε { ActualizacionParaOpt.trad = "" }
ListaExpresiones -> Expresion { ListaExpresiones.trad = Expresion.trad }
ListaExpresiones -> Expresion COMA ListaExpresiones { ListaExpresiones.trad = Expresion.trad + ", " + ListaExpresiones.trad }
SentenciaDeExpresion -> ExpresionDeSentencia PUNTO_Y_COMA { SentenciaDeExpresion.trad = ExpresionDeSentencia.trad + ";" }
ExpresionDeSentencia -> Asignacion { ExpresionDeSentencia.trad = Asignacion.trad }
ExpresionDeSentencia -> InvocacionDeMetodo { ExpresionDeSentencia.trad = InvocacionDeMetodo.trad }
ExpresionDeSentencia -> ExpresionCreacionInstanciaClase { ExpresionDeSentencia.trad = ExpresionCreacionInstanciaClase.trad }
ExpresionCreacionInstanciaClase -> NEW TipoClaseOInterfaz PARENTESIS_IZQ ListaArgumentosOpt PARENTESIS_DER CuerpoClaseOpt { ExpresionCreacionInstanciaClase.trad = "new " + TipoClaseOInterfaz.trad + "(" + ListaArgumentosOpt.trad + ")" }
InvocacionDeMetodo -> Primario PUNTO IDENTIFICADOR PARENTESIS_IZQ ListaArgumentosOpt PARENTESIS_DER { InvocacionDeMetodo.trad = Primario.trad + "." + IDENTIFICADOR.lexema + "(" + ListaArgumentosOpt.trad + ")" }
InvocacionDeMetodo -> IDENTIFICADOR PARENTESIS_IZQ ListaArgumentosOpt PARENTESIS_DER { InvocacionDeMetodo.trad = IDENTIFICADOR.lexema + "(" + ListaArgumentosOpt.trad + ")" }
InvocacionDeMetodo -> SUPER PUNTO IDENTIFICADOR PARENTESIS_IZQ ListaArgumentosOpt PARENTESIS_DER { InvocacionDeMetodo.trad = "super." + IDENTIFICADOR.lexema + "(" + ListaArgumentosOpt.trad + ")" }
InvocacionDeMetodo -> NombreDeExpresion PUNTO IDENTIFICADOR PARENTESIS_IZQ ListaArgumentosOpt PARENTESIS_DER { InvocacionDeMetodo.trad = "cout << " + ListaArgumentosOpt.trad + " << endl" }
ListaArgumentosOpt -> ListaArgumentos { ListaArgumentosOpt.trad = ListaArgumentos.trad }
ListaArgumentosOpt -> ε { ListaArgumentosOpt.trad = "" }
ListaArgumentos -> Expresion { ListaArgumentos.trad = Expresion.trad }
ListaArgumentos -> Expresion COMA ListaArgumentos { ListaArgumentos.trad = Expresion.trad + ", " + ListaArgumentos.trad }
DeclaracionMiembro -> DeclaracionMetodo { DeclaracionMiembro.trad = DeclaracionMetodo.trad }
DeclaracionMiembro -> DeclaracionCampo { DeclaracionMiembro.trad = DeclaracionCampo.trad }
DeclaracionMiembro -> DeclaracionClase { DeclaracionMiembro.trad = DeclaracionClase.trad }
DeclaracionMiembro -> DeclaracionInterfaz { DeclaracionMiembro.trad = DeclaracionInterfaz.trad }
DeclaracionMiembro -> DeclaracionEnumeracion { DeclaracionMiembro.trad = DeclaracionEnumeracion.trad }
DeclaracionMetodo -> Modificadores CabeceraMetodo CuerpoMetodo { DeclaracionMetodo.trad = Modificadores.trad + "\n" + CabeceraMetodo.trad + " " + CuerpoMetodo.trad }
CabeceraMetodo -> TipoOVacio IDENTIFICADOR PARENTESIS_IZQ ListaParametrosFormalesOpt PARENTESIS_DER ClausulaLanzaOpt { CabeceraMetodo.trad = TipoOVacio.trad + " " + IDENTIFICADOR.lexema + "(" + ListaParametrosFormalesOpt.trad + ")" }
TipoOVacio -> Tipo { TipoOVacio.trad = Tipo.trad }
TipoOVacio -> VOID { TipoOVacio.trad = "void" }
CuerpoMetodo -> Bloque { CuerpoMetodo.trad = Bloque.trad }
CuerpoMetodo -> PUNTO_Y_COMA { CuerpoMetodo.trad = ";" }
ListaParametrosFormalesOpt -> ListaParametrosFormales { ListaParametrosFormalesOpt.trad = ListaParametrosFormales.trad }
ListaParametrosFormalesOpt -> ε { ListaParametrosFormalesOpt.trad = "" }
ListaParametrosFormales -> ParametroFormal { ListaParametrosFormales.trad = ParametroFormal.trad }
ListaParametrosFormales -> ParametroFormal COMA ListaParametrosFormales { ListaParametrosFormales.trad = ParametroFormal.trad + ", " + ListaParametrosFormales.trad }
ParametroFormal -> ModificadoresParametroOpt Tipo IDENTIFICADOR { ParametroFormal.trad = Tipo.trad + " " + IDENTIFICADOR.lexema }
ParametroFormal -> ModificadoresParametroOpt Tipo ELLIPSIS IDENTIFICADOR { ParametroFormal.trad = Tipo.trad + "... " + IDENTIFICADOR.lexema }
ModificadoresParametroOpt -> Modificador ModificadoresParametroOpt { ModificadoresParametroOpt.trad = Modificador.trad + " " + ModificadoresParametroOpt.trad }
ModificadoresParametroOpt -> ε { ModificadoresParametroOpt.trad = "" }
CuerpoClase -> LLAVE_IZQ DeclaracionesCuerpoClase LLAVE_DER { CuerpoClase.trad = "{\n" + DeclaracionesCuerpoClase.trad + "\n}" }
DeclaracionesCuerpoClase -> DeclaracionCuerpoClase DeclaracionesCuerpoClase { DeclaracionesCuerpoClase.trad = DeclaracionCuerpoClase.trad + "\n" + DeclaracionesCuerpoClase.trad }
DeclaracionesCuerpoClase -> ε { DeclaracionesCuerpoClase.trad = "" }
DeclaracionCuerpoClase -> DeclaracionMiembro { DeclaracionCuerpoClase.trad = DeclaracionMiembro.trad }
DeclaracionCuerpoClase -> DeclaracionConstructor { DeclaracionCuerpoClase.trad = DeclaracionConstructor.trad }
DeclaracionCuerpoClase -> STATIC Bloque { DeclaracionCuerpoClase.trad = "static " + Bloque.trad }
DeclaracionCuerpoClase -> Bloque { DeclaracionCuerpoClase.trad = Bloque.trad }
DeclaracionCuerpoClase -> PUNTO_Y_COMA { DeclaracionCuerpoClase.trad = "" }
DeclaracionCampo -> Modificadores Tipo DeclaradoresDeVariable PUNTO_Y_COMA { DeclaracionCampo.trad = Modificadores.trad + "\n" + Tipo.trad + " " + DeclaradoresDeVariable.trad + ";" }
DeclaracionConstructor -> Modificadores IDENTIFICADOR PARENTESIS_IZQ ListaParametrosFormalesOpt PARENTESIS_DER ClausulaLanzaOpt CuerpoConstructor { DeclaracionConstructor.trad = Modificadores.trad + "\n" + IDENTIFICADOR.lexema + "(" + ListaParametrosFormalesOpt.trad + ") " + CuerpoConstructor.trad }
CuerpoConstructor -> Bloque { CuerpoConstructor.trad = Bloque.trad }
ParametrosTipoOpt -> ParametrosTipo { ParametrosTipoOpt.trad = ParametrosTipo.trad }
ParametrosTipoOpt -> ε { ParametrosTipoOpt.trad = "" }
ParametrosTipo -> MENOR_QUE ListaParametrosTipo MAYOR_QUE { ParametrosTipo.trad = "<" + ListaParametrosTipo.trad + ">" }
ListaParametrosTipo -> ParametroTipo { ListaParametrosTipo.trad = ParametroTipo.trad }
ListaParametrosTipo -> ParametroTipo COMA ListaParametrosTipo { ListaParametrosTipo.trad = ParametroTipo.trad + ", " + ListaParametrosTipo.trad }
ParametroTipo -> IDENTIFICADOR ListaTiposExtiendeOpt { ParametroTipo.trad = IDENTIFICADOR.lexema + ListaTiposExtiendeOpt.trad }
ListaTiposExtiendeOpt -> EXTENDS ListaTipos { ListaTiposExtiendeOpt.trad = " extends " + ListaTipos.trad }
ListaTiposExtiendeOpt -> ε { ListaTiposExtiendeOpt.trad = "" }
ClausulaExtiendeOpt -> EXTENDS Tipo { ClausulaExtiendeOpt.trad = " : public " + Tipo.trad }
ClausulaExtiendeOpt -> ε { ClausulaExtiendeOpt.trad = "" }
ClausulaImplementaOpt -> IMPLEMENTS ListaTipos { ClausulaImplementaOpt.trad = " , " + ListaTipos.trad }
ClausulaImplementaOpt -> ε { ClausulaImplementaOpt.trad = "" }
ClausulaLanzaOpt -> THROWS ListaNombresCalificados { ClausulaLanzaOpt.trad = "" }
ClausulaLanzaOpt -> ε { ClausulaLanzaOpt.trad = "" }
TipoClaseOInterfaz -> NombreCalificado ArgumentosTipoOpt { TipoClaseOInterfaz.trad = NombreCalificado.trad }
ArgumentosTipoOpt -> ArgumentosTipo { ArgumentosTipoOpt.trad = ArgumentosTipo.trad }
ArgumentosTipoOpt -> ε { ArgumentosTipoOpt.trad = "" }
ArgumentosTipo -> MENOR_QUE ListaArgumentosTipo MAYOR_QUE { ArgumentosTipo.trad = "<" + ListaArgumentosTipo.trad + ">" }
ListaArgumentosTipo -> Tipo { ListaArgumentosTipo.trad = Tipo.trad }
ListaArgumentosTipo -> Tipo COMA ListaArgumentosTipo { ListaArgumentosTipo.trad = Tipo.trad + ", " + ListaArgumentosTipo.trad }
DimensionesTipoOpt -> CORCHETE_IZQ CORCHETE_DER DimensionesTipoOpt { DimensionesTipoOpt.trad = "*" + DimensionesTipoOpt.trad }
DimensionesTipoOpt -> ε { DimensionesTipoOpt.trad = "" }
ListaNombresCalificados -> NombreCalificado { ListaNombresCalificados.trad = NombreCalificado.trad }
ListaNombresCalificados -> NombreCalificado COMA ListaNombresCalificados { ListaNombresCalificados.trad = NombreCalificado.trad + ", " + ListaNombresCalificados.trad }
ListaTipos -> Tipo { ListaTipos.trad = Tipo.trad }
ListaTipos -> Tipo COMA ListaTipos { ListaTipos.trad = Tipo.trad + ", " + ListaTipos.trad }
DeclaracionInterfaz -> Modificadores INTERFACE IDENTIFICADOR ParametrosTipoOpt ClausulaExtiendeOpt CuerpoInterfaz { DeclaracionInterfaz.trad = "class " + IDENTIFICADOR.lexema + " {" }
CuerpoInterfaz -> LLAVE_IZQ DeclaracionesMiembroInterfazOpt LLAVE_DER { CuerpoInterfaz.trad = "{\n" + DeclaracionesMiembroInterfazOpt.trad + "\n}" }
DeclaracionesMiembroInterfazOpt -> DeclaracionesMiembroInterfaz { DeclaracionesMiembroInterfazOpt.trad = DeclaracionesMiembroInterfaz.trad }
DeclaracionesMiembroInterfazOpt -> ε { DeclaracionesMiembroInterfazOpt.trad = "" }
DeclaracionesMiembroInterfaz -> DeclaracionMiembroInterfaz DeclaracionesMiembroInterfaz { DeclaracionesMiembroInterfaz.trad = DeclaracionMiembroInterfaz.trad + "\n" + DeclaracionesMiembroInterfaz.trad }
DeclaracionesMiembroInterfaz -> DeclaracionMiembroInterfaz { DeclaracionesMiembroInterfaz.trad = DeclaracionMiembroInterfaz.trad }
DeclaracionMiembroInterfaz -> Modificadores DeclaracionMetodoDeInterfaz { DeclaracionMiembroInterfaz.trad = Modificadores.trad + " " + DeclaracionMetodoDeInterfaz.trad }
DeclaracionMiembroInterfaz -> DeclaracionCampo { DeclaracionMiembroInterfaz.trad = DeclaracionCampo.trad }
DeclaracionMiembroInterfaz -> DeclaracionClase { DeclaracionMiembroInterfaz.trad = DeclaracionClase.trad }
DeclaracionMiembroInterfaz -> DeclaracionInterfaz { DeclaracionMiembroInterfaz.trad = DeclaracionInterfaz.trad }
DeclaracionMetodoDeInterfaz -> TipoOVacio IDENTIFICADOR PARENTESIS_IZQ ListaParametrosFormalesOpt PARENTESIS_DER ClausulaLanzaOpt CuerpoMetodoOpt { DeclaracionMetodoDeInterfaz.trad = TipoOVacio.trad + " " + IDENTIFICADOR.lexema + "(" + ListaParametrosFormalesOpt.trad + ")" + CuerpoMetodoOpt.trad }
CuerpoMetodoOpt -> CuerpoMetodo { CuerpoMetodoOpt.trad = CuerpoMetodo.trad }
CuerpoMetodoOpt -> ε { CuerpoMetodoOpt.trad = ";" }
DeclaracionEnumeracion -> Modificadores ENUM IDENTIFICADOR CuerpoEnumeracion { DeclaracionEnumeracion.trad = "enum " + IDENTIFICADOR.lexema + " " + CuerpoEnumeracion.trad }
CuerpoEnumeracion -> LLAVE_IZQ ConstantesEnumeracionOpt DeclaracionesCuerpoEnumOpt LLAVE_DER { CuerpoEnumeracion.trad = "{\n" + ConstantesEnumeracionOpt.trad + DeclaracionesCuerpoEnumOpt.trad + "\n}" }
ConstantesEnumeracionOpt -> ConstantesEnumeracion { ConstantesEnumeracionOpt.trad = ConstantesEnumeracion.trad }
ConstantesEnumeracionOpt -> ε { ConstantesEnumeracionOpt.trad = "" }
ConstantesEnumeracion -> ConstanteEnumeracion { ConstantesEnumeracion.trad = ConstanteEnumeracion.trad }
ConstantesEnumeracion -> ConstanteEnumeracion COMA ConstantesEnumeracion { ConstantesEnumeracion.trad = ConstanteEnumeracion.trad + ", " + ConstantesEnumeracion.trad }
ConstanteEnumeracion -> IDENTIFICADOR ArgumentosConstanteEnumOpt CuerpoClaseOpt { ConstanteEnumeracion.trad = IDENTIFICADOR.lexema }
ArgumentosConstanteEnumOpt -> PARENTESIS_IZQ ListaArgumentosOpt PARENTESIS_DER { ArgumentosConstanteEnumOpt.trad = "(" + ListaArgumentosOpt.trad + ")" }
ArgumentosConstanteEnumOpt -> ε { ArgumentosConstanteEnumOpt.trad = "" }
DeclaracionesCuerpoEnumOpt -> COMA DeclaracionesCuerpoEnum { DeclaracionesCuerpoEnumOpt.trad = DeclaracionesCuerpoEnum.trad }
DeclaracionesCuerpoEnumOpt -> DeclaracionesCuerpoEnum { DeclaracionesCuerpoEnumOpt.trad = DeclaracionesCuerpoEnum.trad }
DeclaracionesCuerpoEnumOpt -> ε { DeclaracionesCuerpoEnumOpt.trad = "" }
DeclaracionesCuerpoEnum -> DeclaracionCuerpoClase DeclaracionesCuerpoClase { DeclaracionesCuerpoEnum.trad = DeclaracionCuerpoClase.trad + "\n" + DeclaracionesCuerpoClase.trad }
RestoCreadorDeArreglo -> CORCHETE_IZQ Expresion CORCHETE_DER DimensionesArregloOpt { RestoCreadorDeArreglo.trad = "[" + Expresion.trad + "]" + DimensionesArregloOpt.trad }
RestoCreadorDeArreglo -> CORCHETE_IZQ CORCHETE_DER DimensionesArregloOpt { RestoCreadorDeArreglo.trad = "[]" + DimensionesArregloOpt.trad }
DimensionesArregloOpt -> CORCHETE_IZQ CORCHETE_DER DimensionesArregloOpt { DimensionesArregloOpt.trad = "[]" + DimensionesArregloOpt.trad }
DimensionesArregloOpt -> ε { DimensionesArregloOpt.trad = "" }
CuerpoClaseOpt -> CuerpoClase { CuerpoClaseOpt.trad = CuerpoClase.trad }
CuerpoClaseOpt -> ε { CuerpoClaseOpt.trad = "" }
IdentificadorDeclaradorVariable -> IDENTIFICADOR DimensionesVarOpt { IdentificadorDeclaradorVariable.trad = IDENTIFICADOR.lexema + DimensionesVarOpt.trad }
InicializacionDeclaradorDeVariable -> ASIGNACION InicializadorVariable { InicializacionDeclaradorDeVariable.trad = " = " + InicializadorVariable.trad }
InicializacionDeclaradorDeVariable -> ε { InicializacionDeclaradorDeVariable.trad = "" }
ClausulasCapturaOpt -> ClausulasCaptura { ClausulasCapturaOpt.trad = ClausulasCaptura.trad }
ClausulasCapturaOpt -> ε { ClausulasCapturaOpt.trad = "" }
ClausulasCaptura -> ClausulaCaptura ClausulasCaptura { ClausulasCaptura.trad = ClausulaCaptura.trad + "\n" + ClausulasCaptura.trad }
ClausulasCaptura -> ClausulaCaptura { ClausulasCaptura.trad = ClausulaCaptura.trad }
ClausulaCaptura -> CATCH PARENTESIS_IZQ ParametroFormalCaptura PARENTESIS_DER Bloque { ClausulaCaptura.trad = "catch (" + ParametroFormalCaptura.trad + ") " + Bloque.trad }
ParametroFormalCaptura -> Tipo IdentificadorDeclaradorVariable { ParametroFormalCaptura.trad = Tipo.trad + " " + IdentificadorDeclaradorVariable.trad }
FinalmenteOpt -> FINALLY Bloque { FinalmenteOpt.trad = "finally " + Bloque.trad }
FinalmenteOpt -> ε { FinalmenteOpt.trad = "" }
ParaCadaVariable -> ModificadoresParametroOpt Tipo IDENTIFICADOR { ParaCadaVariable.trad = Tipo.trad + " " + IDENTIFICADOR.lexema }
Sentencia -> SWITCH PARENTESIS_IZQ Expresion PARENTESIS_DER Bloque { Sentencia.trad = "switch (" + Expresion.trad + ") " + Bloque.trad }
Sentencia -> DO Sentencia WHILE PARENTESIS_IZQ Expresion PARENTESIS_DER PUNTO_Y_COMA { Sentencia.trad = "do " + Sentencia.trad + " while (" + Expresion.trad + ");" }
Sentencia -> BREAK IDENTIFICADOR_OPC PUNTO_Y_COMA { Sentencia.trad = "break " + IDENTIFICADOR_OPC.trad + ";" }
Sentencia -> CONTINUE IDENTIFICADOR_OPC PUNTO_Y_COMA { Sentencia.trad = "continue " + IDENTIFICADOR_OPC.trad + ";" }
Sentencia -> THROW Expresion PUNTO_Y_COMA { Sentencia.trad = "throw " + Expresion.trad + ";" }
Sentencia -> TRY Bloque ClausulasCapturaOpt FinalmenteOpt { Sentencia.trad = "try " + Bloque.trad + " " + ClausulasCapturaOpt.trad + " " + FinalmenteOpt.trad }
Sentencia -> CASE Expresion DOS_PUNTOS { Sentencia.trad = "case " + Expresion.trad + ":" }
Sentencia -> DEFAULT DOS_PUNTOS { Sentencia.trad = "default:" }
TOKEN_EOF -> EOF { TOKEN_EOF.trad = "" }